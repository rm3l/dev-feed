/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2020 Armel Soro
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
plugins {
    id "pl.allegro.tech.build.axion-release" version "1.13.6"
    id "com.diffplug.spotless" version "6.2.0"
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.6.10"
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

scmVersion {
    localOnly = true
    repository {
        type = 'git'
        directory = project.rootProject.file('../')
    }
    tag {
        prefix = ''
    }
    snapshotCreator { version, position -> '' }
    hooks {
        pre 'fileUpdate', [
                files: [
                        '../README.md',
                        'Dockerfile',
                        'Procfile'
                ],
                pattern: {v,p -> /dev-feed-api-$v/},
                replacement: {v, p -> "dev-feed-api-$v"}]
        pre 'fileUpdate', [
                files: [
                        'build.sh',
                        'run.sh'
                ],
                pattern: {v,p -> /dev-feed-api\:$v/},
                replacement: {v, p -> "dev-feed-api:$v"}]
        pre 'fileUpdate', [
                file: 'crawlers/cli/src/main/kotlin/org/rm3l/devfeed/crawlers/cli/DevFeedCrawlerCliRunner.kt',
                pattern: {v,p -> /version = \["$v"\]/},
                replacement: {v, p -> "version = [\"$v\"]"}]
        pre 'commit'
    }
}

allprojects {
    project.version = scmVersion.version
}

subprojects {

    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.jetbrains.kotlin.jvm"

    group = 'org.rm3l'
    version = rootProject.scmVersion.version

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    java {
        toolchain{
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    compileKotlin {
        kotlinOptions {
            languageVersion = "1.6"
            freeCompilerArgs = ["-Xjsr305=strict", "-Xopt-in=kotlin.RequiresOptIn"]
            jvmTarget = JavaVersion.VERSION_17.majorVersion
        }
    }
    compileTestKotlin {
        kotlinOptions {
            languageVersion = "1.6"
            freeCompilerArgs = ["-Xjsr305=strict", "-Xopt-in=kotlin.RequiresOptIn"]
            jvmTarget = JavaVersion.VERSION_17.majorVersion
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    spotless { // if you are using build.gradle.kts, instead of 'spotless {' use:
        // configure<com.diffplug.gradle.spotless.SpotlessExtension> {
        kotlin {
            // by default the target is every '.kt' and '.kts` file in the java sourcesets
            ktfmt("0.30")    // has its own section below
//            ktlint()   // has its own section below
//            diktat()   // has its own section below
//            prettier() // has its own section below
//            licenseHeader '/* (C)$YEAR */' // or licenseHeaderFile
        }
    }

    dependencies {

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"

        implementation 'org.apache.commons:commons-lang3:3.12.0'

        implementation("org.jsoup:jsoup:1.14.3")

        implementation("org.jetbrains.exposed:exposed-dao:0.37.3")
        implementation("org.jetbrains.exposed:exposed-jdbc:0.37.3")

        implementation 'com.zaxxer:HikariCP:5.0.1'

        //JDBC Drivers
        implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
        implementation 'com.h2database:h2:2.0.206'
        implementation 'org.postgresql:postgresql:42.3.1'
        implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.4'

        implementation "io.ktor:ktor-client-core:1.6.7"
        //Using Apache Engine Ktor engine, because it is at this time the only one that supports following redirects
        // https://ktor.io/docs/http-client-engines.html#jvm
        implementation "io.ktor:ktor-client-apache:1.6.7"
        implementation "io.ktor:ktor-client-jackson:1.6.7"

        implementation('com.github.chimbori:crux:3.0.1')

        implementation "com.rometools:rome-opml:1.18.0"

        testImplementation(platform('org.junit:junit-bom:5.8.2'))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }
}
